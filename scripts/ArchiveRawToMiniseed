#!/bin/tcsh -f
#
# Convert all .gz (and .GZ) files in the named source dir to
# site/year/day subdirectories in the named destination dir root
# run through dataselect to generate a 'buddir' like dir & file structure


# General setup
set myname = `basename $0` #ArchiveRawToMiniseed

set setup  = ~nrts/scripts/ScriptSetup
if (! -e $setup ) then
    set message = "missing $setup"
    logger -t $myname"[$$]" -p local0.error "$message"
    echo "${myname}: $message"
    goto failure
endif
source $setup

# Check for debug flag

if (-e $DebugDir/$myname) then
    set debug = 1
else
    set debug = 0
endif

if ($debug) logger -t $myname"[$$]" -p local0.debug "begin"

# Command line setup

set error = 0
if ($#argv == 5) then
    set site = `echo $argv[1] | ucase`
    set lsite = `echo $argv[1] | lcase`
    set year  = $argv[2]
    set oday = $argv[3]
    set raw_root_dir = $argv[4]
    set ms_root_dir = $argv[5]
else
    set error = 1
endif

if ($error) then
    set message = "Bad command line, error $error"
    logger -t $myname"[$$]" -p local0.error "$message"
    echo "${myname}: $message"
    echo "usage: $myname site year oday"
    goto failure
endif

# construct src and dst dirs
set rawdir = $raw_root_dir/$lsite/$year/$oday
set msdir = $ms_root_dir/$lsite/$year/$oday
# Nothing to do if src directory doesn't exist

if (! -d $rawdir) then
    logger -t $myname"[$$]" -p local0.error "ERROR: Source dir not found: $rawdir"
    goto failure
endif

# Work in the source directory

cd $rawdir

if ($status != 0) then
    logger -t $myname"[$$]" -p local0.error "ERROR: can't chdir $rawdir"
    goto failure
endif

set gzfiles = (`ls *.{gz,GZ}`)
if ($status != 0) then
    logger =t $myname"[$$]" -p local0.error "No gz files found in $rawdir."
    goto failure
endif


set tmpfn = /tmp/$myname.$lsite.$$.ms
rm -rf $tmpfn >& /dev/null

set command = "cat $gzfiles | imseed sta=$lsite >>! $tmpfn"
if ($debug) logger -t $myname"[$$]" -p local0.debug "$command"

(cat $gzfiles | imseed sta=$lsite >>! $tmpfn) >>& /dev/null

if ($status != 0) then
    logger -t $myname"[$$]" -p local0.error "ERROR: Could not convert gzfiles to miniseed: $lsite $year $oday"
    rm -rf $tmpfn >& /dev/null
    exit 1
endif

if (! -d $msdir ) then
    mkdir -p $msdir   # make sure it exists
    if ($status != 0) then
        logger -t $myname"[$$]" -p local0.error "ERROR: can't mkdir $msdir"
        goto failure
    endif

    chmod g+w $msdir   # make sure it is group writable
    if ($status != 0) then
        logger -t $myname"[$$]" -p local0.error "ERROR: error making $msdir group writable"
        goto failure
    endif
else
    rm -rf $msdir/*.${year}.${oday} >& /dev/null
endif
# run through dataselect

set command = "dataselect -Ps -ts ${year}.${oday}.00.00.00 -te ${year}.${oday}.23.59.59.999999 -A ${msdir}/%n.%s.%l.%c.%Y.%j $tmpfn"
if ($debug) logger -t $myname"[$$]" -p local0.debug "$command"
$command
if ($status != 0) then
    rm -rf $tmpfn >& /dev/null
    logger -t $myname"[$$]" -p local0.error "FAILED: $command"
    goto failure
endif
echo $command >>! ${ms_root_dir}/${myname}.log


set command = "msi -S ${msdir}/II.*.*.*.*.*"
set invfn   = ${msdir}/inventory
if ($debug) logger -t $myname"[$$]" -p local0.debug "$command '>!' $invfn"
$command >! $invfn
set savestat = $status
if ($savestat != 0) then
    logger -t $myname"[$$]" -p local0.error "FAILED: [retcode: $savestat] $command '>!' $invfn"
    goto failure
endif

success:
rm -rf $tmpfn >& /dev/null
if ($debug) logger -t $myname"[$$]" -p local0.debug "completed OK"
exit 0

failure:
logger -t $myname"[$$]" -p local0.error "exit 1"
exit 1

