#!/bin/csh -f
#
# Compress tee files and move them to the archive
set myname = ArchiveTeeFiles

# General setup

set setup = ~nrts/scripts/ScriptSetup
if (! -e $setup ) then
    set message = "missing $setup"
    logger -t $myname"[$$]" -p local0.info "$message"
    echo "${myname}: $message"
    goto failure
endif
source $setup

# Check for debug flag
if (-e $DebugDir/$myname) then
    set debug = 1
else
    set debug = 0
endif

# Check for another running instance
set lock   = /var/tmp/$myname.lock
filelock $lock $$
if ($status != 0) then
    if ($debug) logger -t $myname"[$$]" -p local0.info "another instance already active"
    exit 0
endif


if (($#argv != 2) && ($#argv != 3)) then
    logger -t $myname"[$$]" -p local0.info "bad command line"
    echo "usage: $myname raw_root_dir ms_root_dir [stage_root_dir]"
    goto failure
endif

set RawArchiveDir = $argv[1]
if (! -d $RawArchiveDir) then
    set message = "missing $RawArchiveDir"
    logger -t $myname"[$$]" -p local0.info "$message"
    echo "${myname}: $message"
    goto failure
endif

set MSArchiveDir = $argv[2]
if (! -d $MSArchiveDir) then
    set message = "missing $MSArchiveDir"
    logger -t $myname"[$$]" -p local0.info "$message"
    echo "${myname}: $message"
    goto failure
endif

if ($#argv != 3) then
    set use_staging_dir = 0
else
    set use_staging_dir = 1
    set StageDir = $argv[3]
    if (! -d $StageDir) then
        set message = "missing $StageDir"
        logger -t $myname"[$$]" -p local0.info "$message"
        echo "${myname}: $message"
        goto failure
    endif
endif

if ($debug) logger -t $myname"[$$]" -p local0.info "begin"

# Process each site listed in the Systems file

set systems = $NRTS_HOME/etc/Systems
if (! -e $systems) then
    logger -t $myname"[$$]" -p local0.info "missing $systems file"
    goto failure
endif

set SiteList = `stripcat $systems`
if ($status != 0) then
    logger -t $myname"[$$]" -p local0.info "stripcat $systems FAILED"
    goto failure
endif

foreach site ($SiteList)

    set tee = $NRTS_HOME/$site/tee
    set raw_archive = $RawArchiveDir/$site

    if (! -d $tee) then
        if ($debug) logger -t $myname"[$$]" -p local0.info "Skipping $site. No tee dir found ($tee)."
        continue
    endif

    if (! -d $raw_archive) then
        set command = "mkdir $raw_archive"
        if ($debug) logger -t $myname"[$$]" -p local0.info "$command"
        $command
        if ($status != 0) then
            set message = "can't $command"
            logger -t $myname"[$$]" -p local0.info "$message"
            echo "${myname}: $message"
            goto failure
        endif
    endif

    set command = "GzipTeeFiles $tee"
    if ($debug) logger -t $myname"[$$]" -p local0.info "$command"
    $command

    if ($use_staging_dir == 1) then
       set stage = $StageDir/$site
       set command = "StageGzFiles $tee $stage"
       if (-d $stage) then
           if ($debug) logger -t $myname"[$$]" -p local0.info "$command"
           $command
       else
           if ($debug) logger -t $myname"[$$]" -p local0.info "${stage} not found. Skipping: $command"
       endif
    endif

    set command = "ArchiveGzFiles $tee $site $RawArchiveDir $MSArchiveDir"
    if ($debug) logger -t $myname"[$$]" -p local0.info "$command"
    $command

end

# Normal exit

success:
if ($debug) logger -t $myname"[$$]" -p local0.info "completed OK"
exit 0

failure:
logger -t $myname"[$$]" -p local0.info "exit 1"
exit 1

# Revision History
#
# $Log: ArchiveTeeFiles,v $
#
# Revision 1.11  2020/03/14 18:15:00  dauerbach
# Pass stage_dir as (required) 2nd parameter to remove hard-coded path
#
# Revision 1.10  2015/06/17 17:25:44  dechavez
# changed stage directory to /ida/stage
#
# Revision 1.9  2014/10/02 19:16:10  dechavez
# fixed missing quote
#
# Revision 1.8  2014/10/02 18:43:41  dechavez
# only StageGzFiles if the destination directory exists
#
# Revision 1.7  2014/10/01 22:20:27  dechavez
# Added StageGzFiles for updating /ida/stage as well
#
# Revision 1.6  2013/10/24 18:52:01  dechavez
# create station subdir of archive directory if it doesn't already exist
#
# Revision 1.5  2013/10/21 21:34:08  dechavez
# initial production release
#
