#!/bin/csh -f
# $Id: ArchiveTeeFiles,v 1.10 2015/06/17 17:25:44 dechavez Exp $
#
# Compress tee files and move them to the archive

set myname = ArchiveTeeFiles

if ($#argv != 1) then
    logger -t $myname"[$$]" -p user.info "bad command line"
    echo "usage: $myname archive_dir"
    goto failure
endif

set ArchiveDir = $argv[1]
if (! -d $ArchiveDir) then
    set message = "missing $ArchiveDir"
    logger -t $myname"[$$]" -p user.info "$message"
    echo "${myname}: $message"
    goto failure
endif

# General setup

set setup = ~nrts/scripts/ScriptSetup
if (! -e $setup ) then
    set message = "missing $setup"
    logger -t $myname"[$$]" -p user.info "$message"
    echo "${myname}: $message"
    goto failure
endif
source $setup

# Check for debug flag

if (-e $DebugDir/$myname) then
    set debug = 1
else
    set debug = 0
endif

if ($debug) logger -t $myname"[$$]" -p user.info "begin"

# Process each site listed in the Systems file

set systems = /usr/nrts/etc/Systems
if (! -e $systems) then
    logger -t $myname"[$$]" -p user.info "missing $systems file"
    goto failure
endif

set SiteList = `stripcat $systems`
if ($status != 0) then
    logger -t $myname"[$$]" -p user.info "stripcat $systems FAILED"
    goto failure
endif

foreach site ($SiteList)

    set tee = $NRTS_HOME/$site/tee
    set archive = $ArchiveDir/$site
    if (! -d $archive) then
        set command = "mkdir $archive"
        if ($debug) logger -t $myname"[$$]" -p user.info "$command"
        $command
        if ($status != 0) then
            set message = "can't $command"
            logger -t $myname"[$$]" -p user.info "$message"
            echo "${myname}: $message"
            goto failure
        endif
    endif

    set command = "GzipTeeFiles $tee"
    if ($debug) logger -t $myname"[$$]" -p user.info "$command"
    $command

    set stage = /ida/stage/$site
    if (-d $stage) then
        set command = "StageGzFiles $tee $stage"
        if ($debug) logger -t $myname"[$$]" -p user.info "$command"
        $command
    endif

    set command = "ArchiveGzFiles $tee $archive"
    if ($debug) logger -t $myname"[$$]" -p user.info "$command"
    $command

end

# Normal exit

success:
if ($debug) logger -t $myname"[$$]" -p user.info "completed OK"
exit 0

failure:
logger -t $myname"[$$]" -p user.info "exit 1"
exit 1

# Revision History
#
# $Log: ArchiveTeeFiles,v $
# Revision 1.10  2015/06/17 17:25:44  dechavez
# changed stage directory to /ida/stage
#
# Revision 1.9  2014/10/02 19:16:10  dechavez
# fixed missing quote
#
# Revision 1.8  2014/10/02 18:43:41  dechavez
# only StageGzFiles if the destination directory exists
#
# Revision 1.7  2014/10/01 22:20:27  dechavez
# Added StageGzFiles for updating /ida/stage as well
#
# Revision 1.6  2013/10/24 18:52:01  dechavez
# create station subdir of archive directory if it doesn't already exist
#
# Revision 1.5  2013/10/21 21:34:08  dechavez
# initial production release
#
