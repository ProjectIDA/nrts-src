#!/bin/csh -f
# $Id: isiGenerateWfdiscFiles,v 1.14 2015/12/10 00:00:15 dechavez Exp $
#
# Collect all available NRTS wfdiscs into station and network day files

set debug  = 0
set myname = isiGenerateWfdiscFiles

if ($debug) logger -t $myname"[$$]" -p user.info "begin"

# General setup

set setup  = ~nrts/scripts/ScriptSetup
if (! -e $setup ) then
    set message = "missing $setup"
    logger -t $myname"[$$]" -p user.info "$message"
    echo "${myname}: $message"
    exit 1
endif
source $setup
set envrc  = $NRTS_HOME/.envrc
if (-e $envrc) source $envrc

set workdir = $NRTS_HOME/tmp/$myname.$$
set master  = master.wfdisc
set defout  = $NRTS_HOME/wfdiscs
set lock    = /var/tmp/$myname.lock
set default_wfdir = /usr/nrts

# Command line setup

if ($#argv == 0) then
    set outdir = $defout
    set wfdir  = $default_wfdir
else if ($#argv == 1) then
    set outdir = $argv[1]
    set wfdir  = $default_wfdir
else if ($#argv == 2) then
    set outdir = $argv[1]
    set wfdir  = $argv[2]
else
    set message = "bad command line"
    logger -t $myname"[$$]" -p user.info "$message"
    echo "${myname}: $message"
    exit 1
endif
set sed_string = s\;/usr/nrts\;$wfdir\;

# Make sure no other instance of this script is running

filelock $lock $$
if ($status != 0) then
    if ($debug) logger -t $myname"[$$]" -p user.info "another instance already active"
    goto success
endif

# Create output directory if it does not already exist

if (! -e $outdir) mkdir $outdir

# Create the master and station wfdisc files

mkdir $workdir
set exitstatus = $status
if ($exitstatus != 0) then
    set message = "can't mkdir $workdir : status = $exitstatus"
    logger -t $myname"[$$]" -p user.info "$message"
    goto failure
endif

cd $workdir
set exitstatus = $status
if ($exitstatus != 0) then
    set message = "can't cd $workdir : status = $exitstatus"
    logger -t $myname"[$$]" -p user.info "$message"
    goto failure
endif
cp /dev/null $master

if ($debug) logger -t $myname"[$$]" -p user.info "creating master wfdisc"
set command = "isi server=localhost wd"
if ($debug) logger -t $myname"[$$]" -p user.info "$command"
$command | sed $sed_string | sed 's/ i4 / s4 /' >> $master
set exitstatus = $status
if ($exitstatus != 0) then
    set message = "command failed : status = $exitstatus"
    logger -t $myname"[$$]" -p user.info "$message"
    goto failure
endif
if ( -z $master) goto success

if ($debug) logger -t $myname"[$$]" -p user.info "creating station wfdiscs"
foreach station ($SystemsList)
    set stawfdisc = $station.wfdisc
    rm -f $stawfdisc
    grep ^$station $master > $stawfdisc
    echo "css3.0" > $station
    if ( $?CSS30_DATABASE_PATH ) echo "$CSS30_DATABASE_PATH" >> $station
end

# Create the network day volumes

set dates = `cat $master | wdjdays | sort | uniq | tail -10`

if ($debug) logger -t $myname"[$$]" -p user.info "creating network day wfdiscs"
foreach date ($dates)
    validate $date >& /dev/null
    if ($status == 0) then
        set output = $date.wfdisc
        cp /dev/null $output
        set yyyy = `substring $date 1 4`
        set ddd  = `substring $date 5 3`
        wdcut $yyyy-$ddd $yyyy-$ddd-23:59:59.999 $master >> $output
        echo "css3.0" > $date
        if ( $?CSS30_DATABASE_PATH ) echo $CSS30_DATABASE_PATH >> $date
    endif
end

# Master copy no longer needed

rm $master

# Remove empty wfdiscs

touch dummy.wfdisc
foreach wfdisc (*.wfdisc)
    if (-z $wfdisc) rm $wfdisc
end

# Update calib/calper

setcalib >& /dev/null
# Remove outdated wfdiscs and install new ones

touch $outdir/dummy.wfdisc
rm $outdir/*

touch dummy.wfdisc
mv * $outdir
rm $outdir/dummy.wfdisc

success:
cd $outdir
rm -rf $workdir
if ($debug) logger -t $myname"[$$]" -p user.info "exit 0"
exit 0

failure:
cd $outdir
rm -rf $workdir
logger -t $myname"[$$]" -p user.info "exit 1"
exit 1

# Revision History
#
# $Log: isiGenerateWfdiscFiles,v $
# Revision 1.14  2015/12/10 00:00:15  dechavez
# undid the previous change, now use ~nrts instead of NRTS_HOME to load the setup script
# (didn't work in cron, duh)
#
# Revision 1.13  2015/12/07 22:18:43  dechavez
# don't assume user nrts (use NRTS_HOME environment varible instead)
#
# Revision 1.12  2013/11/05 21:48:46  dechavez
# default wfdir set to /usr/nrts (explict override on command line if you want to change)
#
# Revision 1.11  2013/11/05 21:47:13  dechavez
# override wfdisc dir to either /ida/nrts or user specified
#
# Revision 1.10  2010/06/07 19:46:45  dechavez
# ensure s4 datatype
#
# Revision 1.9  2008/05/05 16:29:05  dechavez
# fixed bug where pfo and xpfo were both ending up in the pfo.wfdisc file
#
# Revision 1.8  2008/03/13 21:34:26  dechavez
# use filelock instead of lockfile (conflicted with a similar program of the same name)
#
# Revision 1.7  2008/03/13 21:22:16  dechavez
# use lockfile for instance checking
#
# Revision 1.6  2008/03/12 21:09:53  dechavez
# improved self detection
#
# Revision 1.5  2008/02/01 22:57:41  dechavez
# use substring instead of non-portable expr substr
# use pgrep -x to locate multiple instances of self
#
# Revision 1.4  2007/02/09 20:13:00  dechavez
# source .envrc file
#
# Revision 1.3  2007/02/09 19:11:36  dechavez
# turned off debug flag
#
# Revision 1.2  2007/02/09 19:10:08  dechavez
# added optional output directory argument
#
# Revision 1.1  2007/02/09 19:00:33  dechavez
# initial release
#
