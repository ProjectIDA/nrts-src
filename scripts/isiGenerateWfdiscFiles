#!/bin/csh -f
# $Id: isiGenerateWfdiscFiles,v 1.15 2017/10/30 19:08:46 dechavez Exp $
#
# Collect all available NRTS wfdiscs into station and network day files

set debug  = 1
set myname = isiGenerateWfdiscFiles

if ($debug) logger -t $myname"[$$]" -p local0.info "begin"

# General setup

set setup  = ~nrts/scripts/ScriptSetup
if (! -e $setup ) then
    set message = "missing $setup"
    logger -t $myname"[$$]" -p local0.info "$message"
    echo "${myname}: $message"
    exit 1
endif
source $setup
set envrc  = $NRTS_HOME/.envrc
if (-e $envrc) source $envrc

set workdir = $NRTS_HOME/tmp/$myname.$$
set master  = master.wfdisc
set defout  = $NRTS_HOME/wfdiscs
set lock    = /var/tmp/$myname.lock
set default_wfdir = /usr/nrts

# Command line setup

if ($#argv == 0) then
    set outdir = $defout
    set wfdir  = $default_wfdir
else if ($#argv == 1) then
    set outdir = $argv[1]
    set wfdir  = $default_wfdir
else if ($#argv == 2) then
    set outdir = $argv[1]
    set wfdir  = $argv[2]
else
    set message = "bad command line"
    logger -t $myname"[$$]" -p local0.info "$message"
    echo "${myname}: $message"
    exit 1
endif
set sed_string = s\;/usr/nrts\;$wfdir\;

# Make sure no other instance of this script is running

filelock $lock $$
if ($status != 0) then
    if ($debug) logger -t $myname"[$$]" -p local0.info "another instance already active"
    goto success
endif

# Create output directory if it does not already exist

if (! -e $outdir) mkdir $outdir

# Create the master and station wfdisc files

mkdir $workdir
set exitstatus = $status
if ($exitstatus != 0) then
    set message = "can't mkdir $workdir : status = $exitstatus"
    logger -t $myname"[$$]" -p local0.info "$message"
    goto failure
endif

cd $workdir
set exitstatus = $status
if ($exitstatus != 0) then
    set message = "can't cd $workdir : status = $exitstatus"
    logger -t $myname"[$$]" -p local0.info "$message"
    goto failure
endif
cp /dev/null $master

if ($debug) logger -t $myname"[$$]" -p local0.info "creating master wfdisc"
set command = "isi server=localhost wd"
if ($debug) logger -t $myname"[$$]" -p local0.info "$command"
$command | sed $sed_string | sed 's/ i4 / s4 /' >> $master
set exitstatus = $status
if ($exitstatus != 0) then
    set message = "command failed : status = $exitstatus"
    logger -t $myname"[$$]" -p local0.info "$message"
    goto failure
endif
if ( -z $master) goto success

if ($debug) logger -t $myname"[$$]" -p local0.info "creating station wfdiscs"
foreach station ($SystemsList)
    set stawfdisc = $station.wfdisc
    rm -f $stawfdisc
    grep ^$station $master > $stawfdisc
end

# Create the network day volumes

set dates = `cat $master | wdjdays | sort | uniq | tail -10`

if ($debug) logger -t $myname"[$$]" -p local0.info "creating network day wfdiscs"
foreach date ($dates)
    validate $date >& /dev/null
    if ($status == 0) then
        set output = $date.wfdisc
        cp /dev/null $output
        set yyyy = `substring $date 1 4`
        set ddd  = `substring $date 5 3`
        wdcut $yyyy-$ddd $yyyy-$ddd-23:59:59.999 $master >> $output
    endif
end

# Master copy no longer needed

rm $master

# Remove empty wfdiscs

touch dummy.wfdisc
foreach wfdisc (*.wfdisc)
    if (-z $wfdisc) rm $wfdisc
end

# Remove outdated wfdiscs and install new ones

touch $outdir/dummy.wfdisc
rm $outdir/*

touch dummy.wfdisc
mv * $outdir
rm $outdir/dummy.wfdisc

success:
cd $outdir
rm -rf $workdir
if ($debug) logger -t $myname"[$$]" -p local0.info "exit 0"
exit 0

failure:
cd $outdir
rm -rf $workdir
logger -t $myname"[$$]" -p local0.info "exit 1"
exit 1

