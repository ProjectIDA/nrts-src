#!/bin/tcsh -f
#
# Find and optionally Kill SSH Tunnels by killing sshd process on Hub for ports used by specified host
#
set myname = `basename $0`

# General setup

set setup  = ~nrts/scripts/ScriptSetup
if (! -e $setup ) then
    set message = "missing $setup"
    logger -t $myname"[$$]" -p local0.info "$message"
    echo "${myname}: $message"
    exit 1
endif
source $setup

# set defaults
set killProcess = 0
set debug = 0

# check args
if (($#argv < 1) || ($#argv > 2)) then
    echo "usage: $myname hostname [ --kill ]"
    exit 1
else
    set hostname = `echo $argv[1] | ucase`
    if ($#argv == 2) then
        if ("$argv[2]" == "--kill") then
            set killProcess = 1
        else
            echo "usage: $myname hostname [ --kill ]"
            exit 1
        endif
    endif
endif

# Check for debug flag
if (-e $DebugDir/$myname) set debug = 1

# set CTRL-C jumo in case I get cold feet
onintr interrupt

# find tunnel ports
set TunnelPorts  = $NRTS_HOME/etc/Tunnels
set hostports = `grep $hostname $TunnelPorts`

if ($status != 0) then
    set msg = "ERROR: host $hostname not found in $TunnelPorts"
    
endif

# echo Hostports: $hostports

set dataport = `echo $hostports | awk '{print $2}'`
set sshport = `echo $hostports | awk '{print $3}'`
set hubhost = "localhost" ## "`hostname`.`dnsdomainname`"

if ($debug) logger -t $myname"[$$]" -p local0.info "Tunnel Info: $hostname $dataport $sshport"

foreach port ($dataport $sshport)

    set sshdprocess = `sudo lsof -i tcp -u tunnel -e /run/user/1000/gvfs | egrep "^sshd.*${hubhost}:$port \(L" | awk '{print $9,$1,"pid:",$2}' | sort | rev | column -t | rev`
    if ("$sshdprocess" != "") then
        set sshdpid = $sshdprocess[4]
        if ($debug) logger -t $myname"[$$]" -p local0.info "found sshd process $sshdpid for: ${hostname}:${port}"
        if ($killProcess) then
            set cmd = "kill -TERM $sshdpid"
            logger -t $myname"[$$]" -p local0.info "$cmd"
            sudo -- tcsh -c "$cmd"
        else
            set msg = "${hostname}:${port} $sshdpid"
            echo $msg
            logger -t $myname"[$$]" -p local0.info "$msg"
        endif
    else
        logger -t $myname"[$$]" -p local0.info "NO sshd process found for: ${hostname}:${port}"
    endif

end

exit 0

interrupt:
    logger -t $myname"[$$]" -p local0.info "User aborted $0 $argv[1]"
    exit 1

