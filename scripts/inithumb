#!/bin/csh -f
#
# $Id: inithumb,v 1.3 2015/12/16 15:34:18 dechavez Exp $
#
# Initialize a thumbdrive (destructive)

# Root only script

set path = (/usr/ucb /usr/bin $path)

set iam = `whoami`
if ($status != 0) then
    echo "FATAL ERROR: whoami not working"
    exit 1
endif
if ($iam != root) then
    echo "Must be run as root (or with sudo)."
    exit 1
endif

# General setup

set setup  = ~nrts/scripts/ScriptSetup
if (! -e $setup ) then
    set message = "missing $setup"
    echo "${myname}: $message"
    goto failure
endif
source $setup
set myname = inithumb

if ($#argv == 0) then
    set device = da0
else if ($#argv == 1) then
    set device = $argv
else
    echo "usage: $myname [device]"
    exit 1
endif

set position = 1
dd if=/dev/zero of=/dev/$device bs=1k count=1 >& /dev/null
if ($status != 0) goto failure

set position = 2
fdisk -BI $device >& /dev/null
if ($status != 0) goto failure

set position = 3
bsdlabel -B -w ${device}s1 auto >& /dev/null
if ($status != 0) goto failure

set position = 4
newfs_msdos /dev/${device}s1
if ($status != 0) goto failure

set position = 5
mount /usb/0
if ($status != 0) goto failure

set position = 6
df -h /usb/0
if ($status != 0) goto failure

set position = 7
umount /usb/0
if ($status != 0) goto failure

echo "Initialization complete.  You may remove the thumbdrive."
exit 0

failure:
echo "Initialization failed with error code $position."
exit 1
