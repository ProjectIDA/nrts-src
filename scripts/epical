#!/bin/csh -f
# $Id: epical,v 1.10 2017/09/08 17:20:59 dechavez Exp $
#
# Run an Episensor RB calibration.
# ASSUMES sensor is on the 00 digitizer, B input
       
set myname    = "epical"

set setup  = $NRTS_HOME/scripts/ScriptSetup
if (! -e $setup ) then
    echo "missing $setup"
    exit 0
endif
source $setup

if ($#argv == 1) then
    set option = $argv[1]
    if ($option == "pulse" || $option == "step") then
        set caltag = step
    else if ($option == "rb" || $option == "rbsm") then
        set caltag = rbsm
    else
        echo "ERROR: unrecognized option '$option'"
        goto usage
    endif
else
    goto usage
endif
       
set log       = ${myname}.log
set sta       = $DefaultSite
set stadir    = $NRTS_HOME/$sta
set caldir    = $stadir/cal
set port      = 4
set cal       = 456
set mon       = 3
set loc       = 00
set input     = B
set tokens    = rbsm${loc}${input}${mon}
set digitizer = ${sta}${loc}
set date1     = `date +%Y-%m%d-%H%M`
set date2     = `date +%Y:%j-%H:%M:%S`
set sensor    = `q330 list | grep ${digitizer} | awk '{print $4}'`
set expected  = episensor
set prefix    = CAL-${digitizer}-${sensor}-${caltag}-${date1}

if ($sensor != $expected) then
    echo "ERROR: $digitizer sensor $input is not '$expected'"
    echo "       Correct $NRTS_HOME/etc/q330.cfg and ensure control lines are correct."
    echo "       If you change control lines you must reboot the digitizer."
    exit 1
endif

if (! -e $caldir) then
    if (-e $stadir) then
        mkdir $caldir
    else
        echo "missing $stadir"
    endif
    exit 1
endif

cd $caldir
if ($status != 0) then
    echo "can't chdir $caldir"
    exit 0
endif

set command   = "qcal ${digitizer}:${port}:${tokens}:${sta} cal=${cal} mon=${mon} prefix=${prefix} $caltag"
echo $date2 - $command >> $log
$command >> $log

exit $status

usage:
echo "usage: ${myname} { pulse | rb }"
exit 1

# $Log: epical,v $
# Revision 1.10  2017/09/08 17:20:59  dechavez
# fixed missing stadir bug that should never have gotten committed in the first place!
#
# Revision 1.9  2016/10/21 17:10:18  dechavez
# sigh.
#
# Revision 1.8  2016/10/21 17:06:08  dechavez
# removed default cal signal... user must specify either pulse or rb
#
# Revision 1.7  2016/10/21 16:52:21  dechavez
# introduced "pulse", "step" options
#
# Revision 1.6  2015/12/07 22:46:16  dechavez
# fixed bug introduced with last "trivial" change that was committed without testing first
#
# Revision 1.5  2015/12/07 22:40:58  dechavez
# attempt to create cal dir if not already there
# include station name in digitizer command string (for the benefit of miniseed output)
#
# Revision 1.4  2015/12/07 20:37:44  dechavez
# removed /usr/nrts expectancies, use new lcq 1.5 token set names
#
# Revision 1.3  2015/08/25 21:10:27  dechavez
# run the rbsm calibration instead of step
#
# Revision 1.2  2015/08/11 20:06:09  dechavez
# fixed bug looking up digitizer sensor B device
#
# Revision 1.1  2015/08/11 19:19:39  dechavez
# first release
#
