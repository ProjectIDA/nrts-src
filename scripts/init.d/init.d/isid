#!/bin/csh -f
#
# start/stop/restart ISI data server
#
# $Id: isid,v 1.9 2011/01/06 19:26:43 dechavez Exp $

set setup = ~nrts/scripts/ScriptSetup
if (! -e $setup) then
    echo "Missing $setup"
    exit 1
endif
source $setup

set isidportfile = ~nrts/etc/isid
if (-e $isidportfile) then
    set portlist = `stripcat $isidportfile`
else
    set portlist = 39136
endif
set mypid = $$

set restart = 0
set chatty = 1

set masterlist = `pgrep -x isid`
set pidlist = ""
foreach pid ($masterlist)
    if ($pid != $$) set pidlist = "$pid $pidlist"
end
if ("$pidlist" == "") shift pidlist

if ($#argv == 1) then
    switch ($argv[1])
      case start:
        goto start
        breaksw

      case stop
        goto stop
        breaksw

      case restart
        set restart = 1
        goto stop
        breaksw

      case bounce
        set restart = 1
        set chatty = 0
        goto stop
        breaksw
    endsw
endif

echo "Usage: /etc/init.d/isid { start | stop | restart }"
exit 1

# Start server

start:

if ($#pidlist != 0) then
    if ($chatty) echo "ISI data server is already running (pid $pidlist)"
else
    foreach port ($portlist)
        set command = "isid port=$port -bd"
        if ($chatty) echo $command
        $command
    end
endif
exit 0

# Stop server

stop:

if ($chatty) echo "Terminating any instances of nrts_edes"
pkill -TERM nrts_edes

if ($#pidlist == 0) then
    if ($chatty) echo "ISI data server is not running"
else
    foreach pid ($pidlist)
        if ($chatty) echo "Stopping isid process $pid"
        kill -TERM $pid
        @ count = 0;
        while ($pid != 0)
            if ($count > 10) then
                if ($chatty) echo "ISI data server (pid $pid) won't die, forced"
                kill -9 $pid
            endif
            sleep 1
            nohup kill -0 $pid >& /dev/null
            if ($status == 1) set pid = 0
        end
    end
    while ($#pidlist != 0) 
        shift pidlist
    end
endif

if ($restart) goto start
exit 0
