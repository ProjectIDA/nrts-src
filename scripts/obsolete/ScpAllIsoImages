#!/bin/csh -f
# $Id: ScpAllIsoImages,v 1.1 2009/10/26 17:50:20 dechavez Exp $
#
# Run isiScpIso for all systems on this host

set myname = ScpAllIsoImages

# General setup

set setup  = ~nrts/scripts/ScriptSetup
if (! -e $setup ) then
    set message = "missing $setup"
    logger -t $myname"[$$]" -p user.info "$message"
    echo "${myname}: $message"
    goto failure
endif
source $setup

# Check for debug flag

if (-e $DebugDir/$myname) then
    set debug = 1
else
    set debug = 0
endif

if ($debug) logger -t $myname"[$$]" -p user.info "begin"

# No arguments allowed

if ($#argv != 0) then
    set message = "bad command line ($argv != 2)"
    if ($debug) logger -t $myname"[$$]" -p user.info "$message"
    echo "${myname}: $message"
    echo "usage: $myname [user@]host remotepath"
    goto failure
endif

# Process each system which has been configured for scp archive

foreach site ($SystemsList)
    set config = $NRTS_HOME/$site/iso/config
    if (! -e $config) continue
    source $config
    if (! $?ScpPath) continue
    if (! $?ScpHost) continue
    if ($?ScpUser) then
        set host = $ScpUser@$ScpHost
    else
        set host = $ScpHost
    endif
    set dest = $ScpPath/$site/`date -u +%Y`
    set command = "ssh $host mkdir -p $dest"
    if ($debug) logger -t $myname"[$$]" -p user.info "$command"
    $command
    if ($status != 0) then
        if ($debug) logger -t $myname"[$$]" -p user.info "command failed"
        goto failure
    endif
    set command = "isiScpIso $site ${host}:$dest"
    if ($debug) logger -t $myname"[$$]" -p user.info "$command"
    $command
end

# Normal exit

success:
pkill -HUP -x ispd
if ($debug) logger -t $myname"[$$]" -p user.info "completed OK"
exit 0

failure:
pkill -HUP -x ispd
logger -t $myname"[$$]" -p user.info "exit 1"
exit 1

# Revision History
#
# $Log: ScpAllIsoImages,v $
# Revision 1.1  2009/10/26 17:50:20  dechavez
# initial release
#
