#!/bin/csh -f
# $Id: isiScpIso,v 1.1 2009/10/26 17:50:20 dechavez Exp $
#
# Copy all ISO files to remote archive

set myname = isiScpIso

# General setup

set setup  = ~nrts/scripts/ScriptSetup
if (! -e $setup ) then
    set message = "missing $setup"
    logger -t $myname"[$$]" -p user.info "$message"
    echo "${myname}: $message"
    goto failure
endif
source $setup

# Check for debug flag

if (-e $DebugDir/$myname) then
    set debug = 1
else
    set debug = 0
endif

if ($debug) logger -t $myname"[$$]" -p user.info "begin"

# Command line setup

set error = 0
if ($#argv == 0) then
    set error = 1
else if ($#argv == 1) then
    if ($#SystemsList == 1) then
        set site = $DefaultSite
        set dest = $argv[1]
    else
        echo "Multi-site systems require explict name of site to process"
        exit 1
    endif
else if ($#argv == 2) then
    set site = $argv[1]
    set dest = $argv[2]
else
    set error = 2
endif

if ($error) then
    set message = "bad command line, error $error"
    if ($debug) logger -t $myname"[$$]" -p user.info "$message"
    echo "${myname}: $message"
    echo "usage: $myname [ site ] [user@]host:path"
    goto failure
endif

set StaName = `echo $site | ucase`

# Get the full path names

set SiteDir    = $NRTS_HOME/$site
set IsoDir     = $SiteDir/iso
set IsoDoneDir = $IsoDir/done/images
set LockFile   = $IsoDir/lock
set StopFlag   = $IsoDir/STOP

# Nothing to do if there is no ISO tree

if (! -e $SiteDir)    goto success
if (! -e $IsoDir)     goto success
if (! -e $IsoDoneDir) goto success

# Nothing to do if the STOP flag is set

if (-e $StopFlag) then
    if ($debug) logger -t $myname"[$$]" -p user.info "quit: $site STOP flag set"
    goto success
endif

# Make sure we are the only process working on this tree

filelock $LockFile $$
if ($status != 0) then
    set active = `cat $LockFile`
    if ($debug) logger -t $myname"[$$]" -p user.info "ISO directory locked by process $active"
    goto success
endif

# Copy all .iso files

cd $IsoDir
set dummy = dummy.iso
touch $dummy
set IsoImageList = `ls *.iso`
rm -f $dummy

foreach IsoImage ($IsoImageList)
    if ($IsoImage == $dummy) continue
    set command = "scp -q $IsoImage $dest"
    if ($debug) logger -t $myname"[$$]" -p user.info $command
    $command
    if ($status != 0) then
        if ($debug) logger -t $myname"[$$]" -p user.info "command failed"
        echo "FAILED: $command"
        goto failure
    endif
    set command = "mv $IsoImage $IsoDoneDir"
    if ($debug) logger -t $myname"[$$]" -p user.info $command
    $command
    if ($status != 0) then
        if ($debug) logger -t $myname"[$$]" -p user.info "command failed"
        echo "FAILED: $command"
        goto StopAndFail
    endif
end

# Normal exit

success:
if ($debug) logger -t $myname"[$$]" -p user.info "completed OK"
exit 0

StopAndFail:
touch $StopFlag
if ($debug) logger -t $myname"[$$]" -p user.info "$StopFlag set"

failure:
logger -t $myname"[$$]" -p user.info "exit 1"
exit 1

# Revision History
#
# $Log: isiScpIso,v $
# Revision 1.1  2009/10/26 17:50:20  dechavez
# initial release
#
