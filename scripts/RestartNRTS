#!/bin/csh -f
# "$Id: RestartNRTS,v 1.2 2016/07/06 20:36:59 dechavez Exp $"
#
# Bounce NRTS
#
# This should really be implemented as an extension of ~scripts/init.d/nrts, but
# that's an "sh" script and I am really busy right now and just want to get
# this done for benefit of ida.adc1 that is having some odd problem with
# memory consumption that I am convinced is hardware related but for which
# no immediate solution is forthcoming.  Therefore here is a quick and dirty
# workaround.

set debug  = 1
set myname = RestartNRTS
set lock   = /var/tmp/$myname.lock

if ($debug) logger -t $myname"[$$]" -p local0.info "begin"

# General setup

set setup  = ~nrts/scripts/ScriptSetup
if (! -e $setup ) then
    set message = "missing $setup"
    logger -t $myname"[$$]" -p local0.info "$message"
    echo "${myname}: $message"
    exit 1
endif
source $setup

# Interval to delay defaults to one minute, unless overridden on the command line

if ($#argv == 0) then
    set delay = 60
else if ($#argv == 1) then
    set delay = $argv[1]
    shift
else
    set message = "${myname}: unrecognized argument(s): $argv"
    echo $message
    if ($debug) logger -t $myname"[$$]" -p local0.info "unrecognized argument(s): $argv"
    exit 1
endif

# Make sure no other instance of this script is running

filelock $lock $$
if ($status != 0) then
    if ($debug) logger -t $myname"[$$]" -p local0.info "another instance already active"
    exit 0
endif

# Bounce NRTS

if ($debug) logger -t $myname"[$$]" -p local0.info "Stopping NRTS"
$NRTS_HOME/scripts/init.d/nrts stop >& /dev/null

if ($debug) logger -t $myname"[$$]" -p local0.info "sleep($delay)"
sleep $delay

if ($debug) logger -t $myname"[$$]" -p local0.info "Starting NRTS"
$NRTS_HOME/scripts/init.d/nrts start >& /dev/null

exit 0

# Revision History
#
# $Log: RestartNRTS,v $
# Revision 1.2  2016/07/06 20:36:59  dechavez
# fixed bug when invoking without any arguments
#
# Revision 1.1  2016/06/29 22:04:27  dechavez
# created
#
