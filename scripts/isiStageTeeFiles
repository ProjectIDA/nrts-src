#!/bin/csh -f
# $Id: isiStageTeeFiles,v 1.17 2013/09/20 17:03:08 dechavez Exp $
#
# Compress and move ISI tee files into ISO staging area

set debug  = 0
set myname = isiStageTeeFiles
set tmp1 = /tmp/$myname.tmp1.$$
set tmp2 = /tmp/$myname.tmp2.$$

if ($debug) logger -t $myname"[$$]" -p local0.info "begin"

# General setup

set setup  = ~nrts/scripts/ScriptSetup
if (! -e $setup ) then
    set message = "missing $setup"
    logger -t $myname"[$$]" -p local0.info "$message"
    echo "${myname}: $message"
    goto failure
endif
source $setup
if ($debug) logger -t $myname"[$$]" -p local0.info "$setup OK"

# Command line setup

set error = 0
if ($#argv == 0) then
    set Force = 0
    set site = $DefaultSite
else if ($#argv == 1) then
    if ($argv[1] == "FORCE") then
        set Force = 1
        if ($#SystemsList == 1) then
            set site = $DefaultSite
        else
            set error = 1
            echo "Multi-site systems require explict name of site to process"
            exit 1
        endif
    else
        set Force = 0
        set site = $argv[1]
    endif
else if ($#argv == 2) then
    if ($argv[1] == "FORCE") then
        set Force = 1
        set site = $argv[2]
    else if ($argv[2] == "FORCE") then
        set Force = 1
        set site = $argv[1]
    else
        set error = 2
    endif
else
    set error = 3
endif

if ($error) then
    set message = "bad command line, error $error"
    logger -t $myname"[$$]" -p local0.info "$message"
    echo "${myname}: $message"
    echo "usage: $myname [ FORCE ] [ site ]"
    goto failure
endif

set StaName = `echo $site | ucase`

# Get the full path names

set SiteDir  = $NRTS_HOME/$site
set TeeDir   = $SiteDir/tee
set IsoDir   = $SiteDir/iso
set StopFlag = $TeeDir/STOP
set LockFile = $IsoDir/lock

# Nothing to do if there is no ISO tree

if (! -e $IsoDir) goto success

# Make sure we are the only process working on this tree

filelock $LockFile $$
if ($status != 0) then
    set active = `cat $LockFile`
    if ($debug) logger -t $myname"[$$]" -p local0.info "ISO directory locked by process $active"
    goto success
endif

# Nothing to do if there isn't a tee directory

if (! -d $TeeDir) then
    if ($debug) logger -t $myname"[$$]" -p local0.info "no such tee dir $TeeDir"
    goto success
endif

# Nothing to do if the STOP flag is set in the tee directory

if (-e $StopFlag) then
    if ($debug) logger -t $myname"[$$]" -p local0.info "quit: $site STOP flag set"
    goto success
endif

# Nothing to do if there isn't a iso directory

if (! -d $IsoDir) then
    if ($debug) logger -t $myname"[$$]" -p local0.info "no such iso dir $IsoDir"
    goto success
endif

# Make sure ISO tree is complete

set StageDir = "$IsoDir/stage"
set DoneZips = "$IsoDir/done/gziped"
set DoneIsos = "$IsoDir/done/images"
set Config   = "$IsoDir/config"

set errors = 0
foreach dir ($StageDir $DoneZips $DoneIsos)
    if (! -d $dir) then
        set message = "Missing $dir"
        logger -t $myname"[$$]" -p local0.info "$message"
        echo "${myname}: $message"
        set errors = 1
    endif
end

# Make required helper scripts exist

set isiPurgeOldImages = $NRTS_HOME/scripts/isiPurgeOldImages
set isiCreateIsoImage = $NRTS_HOME/scripts/isiCreateIsoImage
set isiGenerateIsoLabel = $NRTS_HOME/scripts/isiGenerateIsoLabel
set isiIsoParameters = $IsoDir/config

foreach script ($isiPurgeOldImages $isiCreateIsoImage $isiGenerateIsoLabel $isiIsoParameters)
    if (! -e $script) then
        set message = "Missing $script"
        logger -t $myname"[$$]" -p local0.info "$message"
        echo "${myname}: $message"
        set errors = 1
    endif
end

set isiIsoNotify = $NRTS_HOME/scripts/isiIsoNotify

if ($errors != 0) goto failure

# Clean up old files

$isiPurgeOldImages $IsoDir $DoneIsos
find $DoneZips -atime $DoneZipsFindValue -a -exec rm -f {} \; >& /dev/null
find $DoneIsos -atime $DoneIsosFindValue -a -exec rm -f {} \; >& /dev/null
find $IsoDir -name "*.iso" -mtime $IsoDirFindValue -a -exec rm -f {} \; >& /dev/null

# PktRev, PktLen and DefaultMaxMbytes come from the config file

source $isiIsoParameters

# If forcing an ISO image, then do it now

if ($Force) then
    logger -t $myname"[$$]" -p local0.info "FORCE $StaName $PktRev $PktLen"
    source $isiCreateIsoImage
    if ($debug) logger -t $myname"[$$]" -p local0.info "isiCreateIsoImage status $status"
    if ($status != 0) goto failure
    set count = `ls $IsoDir | grep ".iso" | wc -l`
    echo "$count ISO images ready to copy"
    goto success
else
    @ MaxMBytesPerArchive = $DefaultMaxMbytes
    @ MaxBytesPerArchive = $MaxMBytesPerArchive * 1024 * 1024
    if ($debug) logger -t $myname"[$$]" -p local0.info "begin ($MaxMBytesPerArchive $StaName $PktRev $PktLen)"
endif

# Compress all but the most recent uncompressed tee file

cd $TeeDir
ls [4-9]??????????????????????? >& $tmp1

if ($status == 0) then
    set NumFiles = `cat $tmp1 | wc -l`
    if ($NumFiles <= 0) goto success
    @ NumFiles -= 1
    if ($NumFiles != 0) then
        if ($debug) logger -t $myname"[$$]" -p local0.info "compressing files in $TeeDir"
        set UncompressedFileList = `head -$NumFiles $tmp1`
        if ($status == 0) then
            foreach file ($UncompressedFileList)
                set output = $file.gz
                rm -f $output
                set command = "gzip $file"
                if ($debug) logger -t $myname"[$$]" -p local0.info "$command"
                $command
                if (-e $file && -e $output) then
                    logger -t $myname"[$$]" -p local0.info "$command failed"
                    rm -f $output
                    goto failure
                endif
            end
        endif
    else
        if ($debug) logger -t $myname"[$$]" -p local0.info "nothing pending compression in $TeeDir"
    endif
endif

# Move zipped files into the staging area, building ISO images as needed

StartOfLoop:

    @ BytesUsed  = `du -sk $StageDir | awk '{print $1}'`
    @ BytesUsed *= 1024 # convert from K-bytes to bytes
    if ($BytesUsed > $MaxBytesPerArchive) then
        logger -t $myname"[$$]" -p local0.info "WARNING: $StageDir is too full"
        source $isiCreateIsoImage
        goto StartOfLoop
    endif
    @ AvailSpace = $MaxBytesPerArchive - $BytesUsed

    # Move these files into the iso stage 

    if ($debug) logger -t $myname"[$$]" -p local0.info "staging files from $TeeDir to $StageDir"
    ls [4-9]???????????????????????.gz >& $tmp2
    if ($status == 0) then
        set CompressedFileList = `cat $tmp2`
        foreach file ($CompressedFileList)
            @ FileSize = `wc -c $file | awk '{print $1}'`
            if ($FileSize > $AvailSpace) then
                logger -t $myname"[$$]" -p local0.info "$StageDir is full"
                source $isiCreateIsoImage
                goto StartOfLoop
            else
                set destpath = `uniqpath suflen=3 $file $StageDir`
                set command = "mv $file $destpath"
                if ($debug) logger -t $myname"[$$]" -p local0.info "$command"
                $command
                if ($status == 0) then
                    @ AvailSpace -= $FileSize
                    if ($AvailSpace <= 0) then
                        logger -t $myname"[$$]" -p local0.info "$StageDir is full"
                        source $isiCreateIsoImage
                        goto StartOfLoop
                    endif
                else
                    logger -t $myname"[$$]" -p local0.info "FAILED: $command"
                    goto failure
                endif
            endif
        end
    endif

# Normal exit

success:
rm -f $tmp1
rm -f $tmp2
pkill -HUP -x ispd
if ($debug) logger -t $myname"[$$]" -p local0.info "completed OK"
exit 0

failure:
rm -f $tmp1
rm -f $tmp2
pkill -HUP -x ispd
logger -t $myname"[$$]" -p local0.info "exit 1"
exit 1

