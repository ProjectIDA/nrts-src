#!/bin/csh -f
# $Id: trig,v 1.3 2015/11/18 00:25:42 dechavez Exp $
# Manual control of Q330 trigger

# General setup

set setup  = ~nrts/scripts/ScriptSetup
if (! -e $setup ) then
    echo "${myname}: missing $setup"
    goto failure
endif
source $setup

if ($#argv == 0) then
    set report = 1
else if ($#argv == 1) then
    set report = 0
else
    goto help
endif

set sta = $DefaultSite
set meta = ~nrts/${sta}330/meta
if (! -e $meta) then
    echo "Station '$sta' is not configured as a Q330 based site."
    exit 0
endif

set trigger = $meta/trigger
if (-e $trigger) then
    set on = 1
else
    set on = 0
endif

if ($report) then
    if ($on) then
        echo "Station '$sta' trigger is ON"
    else
        echo "Station '$sta' trigger is OFF"
    endif
    exit 0
endif

if ($argv[1] == "on") then
    if ($on) then
        echo "Station '$sta' trigger is already on."
        exit 0
    endif
    touch $trigger
    echo "Station '$sta' trigger activated."
    exit 0
endif

if ($argv[1] == "off") then
    if (! $on) then
        echo "Station '$sta' trigger is already off."
        exit 0
    endif
    rm -f $trigger
    echo "Station '$sta' trigger terminated."
    exit 0
endif

if ($argv[1] == "pulse") then
    if (-e $trigger) exit 0
    touch $trigger
    sleep 5
    rm -f $trigger
    exit 0
endif

echo "Unrecognized argument: $argv[1]"
goto help

help:
    echo "usage: trig sta [ on | off ]"
    exit 1

failure:
     exit 1

# Revision History
#
# $Log: trig,v $
# Revision 1.3  2015/11/18 00:25:42  dechavez
# changed pulse option to have no effect if trigger is already active
#
# Revision 1.2  2015/11/18 00:05:59  dechavez
# automatically determine station name, added pulse option
#
# Revision 1.1  2008/01/15 04:39:41  dechavez
# initial release
#
