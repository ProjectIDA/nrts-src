#!/bin/tcsh -f
# $Id: StageGzFiles,v 1.1 2014/10/01 22:21:08 dechavez Exp $
#
# Copy all .gz (and .GZ) tee files from the named source dir to
# the named destination directory.  The source files are left
# in place for ArchiveGzFiles to deal with.

set myname = StageGzFiles

# General setup

set setup  = ~nrts/scripts/ScriptSetup
if (! -e $setup ) then
    set message = "missing $setup"
    logger -t $myname"[$$]" -p local0.info "$message"
    echo "${myname}: $message"
    goto failure
endif
source $setup

# Check for debug flag

if (-e $DebugDir/$myname) then
    set debug = 1
else
    set debug = 0
endif

if ($debug) logger -t $myname"[$$]" -p local0.info "begin"

# Command line setup

set error = 0
if ($#argv == 2) then
    set srcdir = $argv[1]
    set dstdir = $argv[2]
else
    set error = 1
endif

if ($error) then
    set message = "bad command line, error $error"
    logger -t $myname"[$$]" -p local0.info "$message"
    echo "${myname}: $message"
    echo "usage: $myname srcdir dstdir"
    goto failure
endif

if ($debug) logger -t $myname"[$$]" -p local0.info "srcdir=$srcdir dstdir=$dstdir"

# Nothing to do if the directories don't exist

foreach dir ($srcdir $dstdir)
    if (! -d $dir) then
        logger -t $myname"[$$]" -p local0.info "ERROR: no such dir $dir"
        set error = 1
    endif
end

if ($error) then
    logger -t $myname"[$$]" -p local0.info "ERROR: required directories are missing"
    goto failure
endif

# Work in the source directory

cd $srcdir
if ($status != 0) then
    logger -t $myname"[$$]" -p local0.info "ERROR: can't chdir $srcdir"
    goto failure
endif

set dummy = 4_______________________.gz
touch $dummy

foreach file ([4-9]???????????????????????.gz [4-9]???????????????????????.GZ)
    if ($file != $dummy) then
        set command = "cp -p $file $dstdir"
        if ($debug) logger -t $myname"[$$]" -p local0.info "$command"
        $command
        if ($status != 0) then
            logger -t $myname"[$$]" -p local0.info "FAILED: $command"
            rm $dummy
            goto failure
        endif
        chmod 644 $dstdir/$file
    endif
end
rm -f $dummy

# Normal exit

success:
if ($debug) logger -t $myname"[$$]" -p local0.info "completed OK"
exit 0

failure:
logger -t $myname"[$$]" -p local0.info "exit 1"
exit 1

# Revision History
#
# $Log: StageGzFiles,v $
# Revision 1.1  2014/10/01 22:21:08  dechavez
# created
#
