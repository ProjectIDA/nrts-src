#!/bin/csh -f
# $Id: isiUpdateSubset,v 1.2 2014/05/09 18:14:27 dechavez Exp $
#
# Update disk loop from remote, using NRTS as stream control list
# Breaks connection when current.  This is intended for use over
# occasional, expensive links (such as Iridium OpenPort)

set debug  = 1
set myname = isiUpdateSubset

if ($debug) logger -t $myname"[$$]" -p user.info "begin"

# General setup

set setup  = ~nrts/scripts/ScriptSetup
if (! -e $setup ) then
    set message = "missing $setup"
    logger -t $myname"[$$]" -p user.info "$message"
    echo "${myname}: $message"
    goto failure
endif
source $setup
if ($debug) logger -t $myname"[$$]" -p user.info "$setup OK"

if ($#argv != 2) then
    set message = "bad command line"
    logger -t $myname"[$$]" -p user.info "$message"
    echo "${myname}: $message"
    echo "usage: $myname server sta"
    goto failure
endif

set  server = $argv[1]
set     sta = $argv[2]
set timeout = 60
set  maxdur = 600

set command = "isidl isi=${server} $sta end=newest scl=nrts log=syslogd:user to=$timeout maxdur=$maxdur"
if ($debug) logger -t $myname"[$$]" -p user.info "$command"
$command

if ($debug) logger -t $myname"[$$]" -p user.info "completed OK"
exit 0

failure:
logger -t $myname"[$$]" -p user.info "exit 1"
exit 1

# Revision History
#
# $Log: isiUpdateSubset,v $
# Revision 1.2  2014/05/09 18:14:27  dechavez
# added maxdur and timeout options
#
# Revision 1.1  2011/04/22 17:56:52  dechavez
# initial release
#
