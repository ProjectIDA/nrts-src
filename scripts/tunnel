#!/bin/csh -f
# $Id$
#
# manage the ALE tunnel contol flags

# General setup

set setup  = ~nrts/scripts/ScriptSetup
if (! -e $setup ) then
    echo "ERROR: missing $setup"
    exit 1
endif
source $setup

# Create the tunnel flag directory if necessary

mkdir -p $TunnelFlagDir >& /dev/null
if (! -d $TunnelFlagDir) then
    echo "ERROR: missing $TunnelFlagDir"
    exit 1
endif

# Set/clear appropriate files

if ($#argv == 0) goto report
if ($#argv != 1) goto usage

if ($argv[1] == enable) then
    set command = "rm -f $TunnelStopFlag"
else if ($argv[1] == disable) then
    set command = "touch $TunnelStopFlag"
else if ($argv[1] == keep) then
    set command = "touch $TunnelKeepAliveFlag"
else if ($argv[1] == normal) then
    set command = "rm -f $TunnelStopFlag $TunnelKeepAliveFlag"
else
    echo "unrecognized argument '$argv[1]'"
    goto usage
endif

$command 
if ($status != 0) then
    echo "ERROR: $command failed"
    exit 1
endif

report:
if (-e $TunnelStopFlag) then
    echo "tunnel disabled"
    exit 0
endif
echo -n "tunnel enabled"

if (-e $TunnelKeepAliveFlag) then
    echo ", keep alive is set"
else
    echo ""
endif
exit 0

usage:
echo "usage: tunnel [ enable | disable | keep | normal ]"
exit 1

# Revision History
#
# $Log$
