#!/bin/csh -f
# $Id: isiIsoToThumbdrive,v 1.9 2015/12/10 00:00:15 dechavez Exp $
#
# Interactive script to copy ISI generated ISO images to USB thumbdrive

set debug  = 0
set myname = isiIsoToThumbdrive

if ($debug) logger -t $myname"[$$]" -p local0.info "begin"

# General setup

set setup  = ~nrts/scripts/ScriptSetup
if (! -e $setup ) then
    echo "${myname}: missing $setup"
    goto failure
endif
source $setup

# Command line setup

set error = 0
if ($#argv == 0) then
    set site = $DefaultSite
else if ($#argv == 1) then
    set site = $argv[1]
else
    set error = 1
endif

if ($error) then
    echo "bad command line, error $error"
    echo "usage: $myname [ site ]"
    goto failure
endif

# Copy the data for the named site

CopyAllImages:

@ count = 0

set SiteDir = $NRTS_HOME/$site
set IsoDir  = $SiteDir/iso
set DoneIsos = "$IsoDir/done/images"
if (-e $IsoDir) then
    set command = "mkdir -p $DoneIsos"
    logger -t $myname"[$$]" -p local0.info "$command"
    $command
    if ($status != 0) then
        logger -t $myname"[$$]" -p local0.info "FAILED"
        goto failure
    endif
    cd $IsoDir
    set dummy = Dummy.iso
    touch $dummy
    set images = `ls *.iso`
    rm -rf $dummy
else
    set images = $dummy
endif

# Copy each image, one by one

foreach image ($images)

    if ($image == $dummy) continue
    @ count++

    @ CopyCount = 1
    echo ""
    set src = $IsoDir/$image
    echo " About to copy ISO image $src"

CopyImage:

#     Mount thumbdrive

    set usb = /usb/0

    echo -n " Insert a thumbdrive and press enter when ready (or quit to abort) "
    set answer = $<
    if ($answer == q || $answer == quit) goto PrematureExit
    sudo umount $usb >& /dev/null
    sudo mount $usb >& /dev/null
    set exitstatus = $status
    if ($exitstatus != 0) then
        echo " *** Unable to mount thumbdrive as $usb ***"
        set exitstatus = 1
        goto TryAgain
    endif

#     Create output directory

    set dest = $usb/$site
    mkdir $usb/$site >& /dev/null
    if (! -d $dest) then
        echo " *** Unable to create destination directory $dest ***"
        set exitstatus = 1
        goto TryAgain
    endif

#     Create Windows "autorun" files

    isiLoadAutorun $dest
    if ($status != 0) then
        echo " *** Unable to create Windows autorun files for $dest ***"
        set exitstatus = 1
        goto TryAgain
    endif

#     Copy image

    echo -n " Copying $src to $dest..."
    cp $src $dest >& /dev/null
    if ($status == 0) then
        echo " done"
        set exitstatus = 0
    else
        echo " FAILED"
        echo " *** Unable to copy ISO image ***"
        rm -f $dest/$image
        set exitstatus = 1
    endif

TryAgain:
    sync
    sudo umount $usb >& /dev/null
    sleep 2
    if ($exitstatus != 0) then
        set result = FAILURE
        echo -n " Do you wish to try again? "
        set answer = $<
        if ($answer == y || $answer == yes) then
            goto CopyImage
        endif
        if ($answer == n || $answer == no ) then
            if ($CopyCount > 1) then
                set result = SUCCESS
                goto ImageCopyAttemptCompleted
            else
                goto PrematureExitWithErrors
            endif
        endif
        echo " Please answer yes or no."
        goto TryAgain
    else
        set result = SUCCESS
        echo " $src copied OK."
    endif

ImageCopyAttemptCompleted:
    if ($result == SUCCESS) then
        pkill -HUP -x -u nrts ispd
        pkill -HUP -x -u nrts ispDisplay
        echo " You may safely remove the thumbdrive."
    else
        goto TryAgain
    endif
end # foreach image

if ($count == 0) goto NormalExit
echo ""

# Make additional copies if desired

MakeAnotherCopy:

echo -n " Do you wish to make another copy? "
set answer = $<
if ($answer == y || $answer == yes) goto CopyAllImages
if ($answer == n || $answer == no ) goto HideAllImages
echo " Please answer yes or no."
goto MakeAnotherCopy

# Move copied images to "done" directory

HideAllImages:

set SiteDir = $NRTS_HOME/$site
set IsoDir  = $SiteDir/iso
set DoneIsos = "$IsoDir/done/images"
if (-e $DoneIsos) then
    cd $IsoDir
    set dummy = Dummy.iso
    touch $dummy
    set images = `ls *.iso`
    rm -rf $dummy
else
    set images = $dummy
endif

foreach image ($images)
    if ($image == $dummy) continue
    set src = $IsoDir/$image
    mv $src $DoneIsos
end # foreach images

NormalExit:
pkill -HUP -x -u nrts ispd
echo ""
if ($count == 0) then
    echo " No ISO images are ready.  If desired, you may use the 'fb'"
    echo " command to force the staged files into a short ISO volume"
    echo " which you may then copy to the thumbdrive."
    echo ""
endif

exit 0

PrematureExitWithErrors:
echo ""
echo " ISO archive copy terminated prematurely due to errors."
echo " You may safely remove the thumbdrive."
PrematureExit:
pkill -HUP -x -u nrts ispd
echo ""

exit 0

failure:
pkill -HUP -x -u nrts ispd
echo ""
echo "*************************************************************************"
echo "SERIOUS PROBLEM: Unable to execute $myname script due to unexpcted errors"
echo "*************************************************************************"
echo ""
echo "Please note any error messages and contact IDA for assistance."
echo ""

exit 1

