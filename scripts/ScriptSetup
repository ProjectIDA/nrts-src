# General purpose setup for NRTS/ISP scripts

# Ensure the required ancillary files are all present

foreach file (.platform .envrc etc/retain)
    set reqfile = ~nrts/$file
    if ( -e $reqfile ) then
        source $reqfile
    else
        echo "FATAL ERROR: missing $reqfile"
        exit 1
    endif
end

if (-e $NRTS_HOME/UNCONFIGURED) exit 0

set status = 0

set path = ($NRTS_HOME/bin     $path)
set path = ($NRTS_HOME/scripts $path)
if (-e /opt/local/bin) set path = (/opt/local/bin $path)
if (-e /usr/local/bin) set path = (/usr/local/bin $path)

set SystemsFile = $NRTS_HOME/etc/Systems
if (-e $SystemsFile && -e $NRTS_HOME/bin/stripcat) then
    set SystemsList = `stripcat $SystemsFile`
    @ SystemsCount = $#SystemsList
    set DefaultSite = $SystemsList[1]
else
    set SystemsList = "empty"
    @ SystemsCount = 0
    set DefaultSite = "DEFAULTSITE"
endif

set DebugDir = $NRTS_HOME/etc/debug

set LogFacility = local0

set TunnelFlagDir       = $NRTS_HOME/etc/.tunnel
set TunnelStopFlag      = $TunnelFlagDir/stop
set TunnelKeepAliveFlag = $TunnelFlagDir/keepalive
# Revision History
#
# $Log: ScriptSetup,v $
# Revision 1.24  2017/01/13 17:34:44  dechavez
# Sigh.  Fixed at Set instead of set error in the change just introduced.  doh
#
# Revision 1.23  2017/01/13 17:28:54  dechavez
# introduced LogFacility, with initial value of local0.  The intent is for
# scripts which user the logger command to user $LogFacility instead of the
# currently hard-coded user or local0 facilities.  That will allow one to
# do a global change of the logging facility without having to edit every
# single script, which is what I need to do now to add the generalization
#
