#!/bin/csh -f
# $Id: AutoCal,v 1.1 2010/10/04 16:45:29 dechavez Exp $
#
# Wrapper script for running automatic calibrations

set myname = AutoCal

# General setup

set setup  = ~nrts/scripts/ScriptSetup
if (! -e $setup ) then
    set message = "missing $setup"
    logger -t $myname"[$$]" -p user.info "$message"
    echo "${myname}: $message"
    goto failure
endif
source $setup

# Check for debug flag

if (-e $DebugDir/$myname) then
    set debug = 1
else
    set debug = 0
endif

if ($debug) logger -t $myname"[$$]" -p user.info "begin"

# Command line setup: AutoCal site digitizer tag

set error = 0
if ($#argv != 3 && $#argv != 4) then
    logger -t $myname"[$$]" -p user.info "bad command line"
    echo "usage: $myname site digitizer caltag [ e300 ]"
    goto failure
endif

set site = $argv[1]
set q330 = $argv[2]
set tag  = $argv[3]
if ($#argv == 4) then
    set e300 = $argv[4]
else
    set e300 = 0
endif

set base = $NRTS_HOME/$site/cal/$myname
set tmp = $base/tmp
set new = $base/new
set old = $base/old

# Verify tree is in place

foreach dir ($tmp $new $old)
    if (! -d $dir) then
        logger -t $myname"[$$]" -p user.info "missing $dir"
        goto failure
    endif
end

# Clean up old runs

foreach dir ($tmp $new $old)
    find $dir -atime $DoneZipsFindValue -a -exec rm -f {} \; >& /dev/null
end

# Launch calibration

# Set up working directory

set workdir = $tmp/$$
mkdir $workdir
if ($status != 0) then
    logger -t $myname"[$$]" -p user.info "can't mkdir $workdir"
    goto failure
endif

cd $workdir
if ($status != 0) then
    logger -t $myname"[$$]" -p user.info "can't chdir $workdir"
    goto failure
endif
if ($debug) logger -t $myname"[$$]" -p user.info "working directory is $workdir"

set log = $workdir/README
touch $log > /dev/null
if ($status != 0) then
    logger -t $myname"[$$]" -p user.info "can't create $log"
    goto failure
endif

# Launch the calibration

set command = "qcal $q330 $tag"
if ($debug) logger -t  $myname"[$$]" -p user.info "$command"
echo `/bin/date +%Y:%j-%H:%M:%S` $command >> $log
$command >>& $log

if ($status != 0) then
    logger -t $myname"[$$]" -p user.info "qcal FAILED.  Log file is $log."
    goto failure
endif

# Move everything into the new directory for remote pickup

mv CAL* $new >>& $log
if ($status != 0) then
    logger -t $myname"[$$]" -p user.info "FAILED to move qcal files to $new.  Log file is $log."
    goto failure
endif
cd $base >>& $log

# Remove working directory

rm -rf $workdir >& /dev/null
if ($status != 0) then
    logger -t $myname"[$$]" -p user.info "FAILED to remove $workdir."
    goto failure
endif

# All done

if ($debug) logger -t  $myname"[$$]" -p user.info "exit 0"
exit 0

failure:
if ($debug) logger -t  $myname"[$$]" -p user.info "exit 1"
exit 1

# Revision History
#
# $Log: AutoCal,v $
# Revision 1.1  2010/10/04 16:45:29  dechavez
# initial release
#
