#!/bin/csh -f
#

set myname = "UpdateNrtsScripts"

set iam = `whoami`
if ($status != 0) then
    echo "FATAL ERROR: whoami not working"
    exit 1
endif
if ($iam != nrts) then
    echo "Must be run as user nrts"
    exit 1
endif

# General setup

set setup  = ~nrts/scripts/ScriptSetup
if (! -e $setup ) then
    set message = "missing $setup"
    echo "${myname}: $message"
    goto Failure
endif
source $setup

# Figure the source

if (! $?SRC_ROOT) then
    echo "${myname}: SRC_ROOT not set"
    goto Failure
endif

set Source = /usr/home/ida/$SRC_ROOT/src/scripts
if (! -e $Source) then
    echo "${myname}: missing $Source"
    goto Failure
endif

set Dest = ~nrts/scripts
cd $Dest
if ($status != 0) then
    echo "can't chdir $Dest"
    goto Failure
endif

foreach file (*)
    if (-e $Source/$file) then
        rm -f $Dest/$file
        cp -p $Source/$file $Dest/$file
        if ($status != 0) then
            echo "can't cp -p $Source/$file $Dest/$file"
            goto Failure
        endif
        echo $file
        chmod 555 $Dest/$file
    else
        echo "Warning: $Dest/$file NOT updated"
    endif
end

Success:
exit 0

Failure:
exit 1

