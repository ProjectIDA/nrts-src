#!/bin/csh
#
#  Roll the NRTS log file(s). 

# General setup

set myname = nrts_flushlog

set setup  = ~nrts/scripts/ScriptSetup
if (! -e $setup ) then
    echo "${myname}: missing $setup"
    goto failure
endif
source $setup

set mode      = 664
set adm       = nrtsmgr
set tmp       = /tmp/$myname.$$
set nrtslog   = $NRTS_HOME/log/nrtslog
set yyyyddd   = `gmt yyyyddd`
set Systems   = $NRTS_HOME/etc/Systems
set Processes = $NRTS_HOME/etc/Processes

# Save existing log file and make an empty new one

set facility = local0

logger -t $myname -p $facility.info -i "flushing log"
mv $nrtslog  $tmp
cp /dev/null $nrtslog
chmod $mode  $nrtslog

# Let syslogd know about the new log file

# FreeBSD 7 (???)
if (-e /usr/bin/syslogd) set syslogd = syslogd

# FreeBSD 10.1
if (-e /usr/sbin/syslogd) set syslogd = syslogd

# RHEL 6
if (-e /sbin/rsyslogd) set syslogd = rsyslogd

sudo /usr/bin/pkill -HUP $syslogd

logger -t $myname -p $facility.info -i "log flushed"

# Split the saved log into pieces

# Process logs

set tempname = $Processes.`hostname`
if (-e $tempname) set Processes = $tempname
if (-e $Processes) then
    set list = `cat $Processes | stripcat`
    if ($status != 0) then
        logger -t $myname -p $facility.info -i "stripcat $Processes failed"
        exit 0
    endif
else
    set list = ""
endif

foreach process ($list)
    set logdir = $NRTS_HOME/log/$process
    mkdir -p $logdir >& /dev/null
    if (-e $logdir) then
        set log = $logdir/$yyyyddd
        touch $log
        fgrep -i $process $tmp >> $log
        chmod $mode $log
        find $logdir -mtime $FlushLogFindValue -a -exec rm -f {} \;
    endif
end

# System logs

set tempname = $Systems.`hostname`
if (-e $tempname) set Systems = $tempname
if (-e $Systems) then
    set list = `cat $Systems | stripcat`
    if ($status != 0) then
        logger -t $myname -p $facility.info -i "stripcat $Systems failed"
        exit 0
    endif
else
    set list = ""
endif

foreach sta ($list)
    set STA = `echo $sta | ucase`
    set logdir = $NRTS_HOME/$sta/log
    if (-e $NRTS_HOME/$sta) mkdir -p $logdir >& /dev/null
    if (-e $logdir) then
        set log = $logdir/$yyyyddd
        touch $log
        fgrep $STA\  $tmp >> $log
        chmod $mode $log
        find $logdir -mtime $FlushLogFindValue -a -exec rm -f {} \;
    endif
end

# Remove the saved log

rm -f $tmp

exit 0

