#!/bin/csh -f
# $Id: isiInitDiskLoop,v 1.1 2009/12/14 19:57:53 dechavez Exp $
#
# Init a disk loop (delete and create)

set myname = isiInitDiskLoop

# General setup

set setup  = ~nrts/scripts/ScriptSetup
if (! -e $setup ) then
    echo "missing $setup"
    goto failure
endif
source $setup

# Command line setup

set error = 0
if ($#argv == 0) then
    echo "usage: $myname site [...]"
    exit 1
else 
    set SiteList = "$argv"
endif

# Loop for each site

foreach site ($SiteList)

    echo $site | grep 330 > /dev/null
    if ($status == 0) then
        set type = qdp
    else
        set type = ida
    endif

    set SiteDir  = $NRTS_HOME/$site

    set ItemList  = ""
    set ItemList  = "$ItemList $SiteDir/bwd"
    set ItemList  = "$ItemList $SiteDir/hdr"
    set ItemList  = "$ItemList $SiteDir/dat"
    set ItemList  = "$ItemList $SiteDir/sys"
    set ItemList  = "$ItemList $SiteDir/isi"

    rm -rf $ItemList

    isimkdl type=$type $site >& /dev/null
    if ($status != 0) then
        echo "unable to create '$site' ISI diskloop"
        exit 1
    endif

    if ($type == ida && -e $SiteDir/cnf) then
        nrts_mkdl $site >& /dev/null
        if ($status != 0) then
            echo "unable to create '$site' NRTS diskloop"
            exit 1
        endif
    endif

end

exit 0

# Revision History
#
# $Log: isiInitDiskLoop,v $
# Revision 1.1  2009/12/14 19:57:53  dechavez
# initial release
#
