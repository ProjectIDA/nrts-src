#!/bin/csh -f
# $Id: isiPurgeOldImages,v 1.4 2014/01/29 21:39:34 dechavez Exp $
#
# Move old, unburned ISO images to done area

set debug  = 0
set myname = isiPurgeOldImages
set dummy  = ________.iso

if ($debug) logger -t $myname"[$$]" -p user.info "begin"

# General setup

set setup  = ~nrts/scripts/ScriptSetup
if (! -e $setup ) then
    set message = "missing $setup"
    logger -t $myname"[$$]" -p user.info "$message"
    echo "${myname}: $message"
    goto failure
endif
source $setup
if ($debug) logger -t $myname"[$$]" -p user.info "$setup OK"

# Command line setup

if ($#argv != 2) then
        echo "usage: $myname IsoDir DoneIsoDir"
        goto failure
endif

set IsoDir     = $argv[1]
set DoneIsoDir = $argv[2]

# Work in the ISO directory

cd $IsoDir
if ($status != 0) then
    echo "ERROR: can't chdir $IsoDir"
    goto failure
endif

# Count the ISO images.  Convoluted stuff at the front is to allow for
# the case where there are zero unburned images waiting.

touch $dummy
set list = ????????.iso
@ numiso = `echo $list | wc -w`
rm -f $dummy
if ($numiso == 1) goto success

# At this point we know we have at least one real ISO image

set list = ????????.iso
@ numiso = `echo $list | wc -w`

while ($numiso > $MaxUnburnedISOImages)
    if ($list[1] != $dummy) then
        set command = "mv $list[1] $DoneIsoDir"
        if ($debug) logger -t $myname"[$$]" -p user.info "$command"
        $command
        shift list
        @ numiso--
    endif
end

# Normal exit

success:
if ($debug) logger -t $myname"[$$]" -p user.info "completed OK"
exit 0

failure:
logger -t $myname"[$$]" -p user.info "exit 1"
exit 1

# Revision History
#
# $Log: isiPurgeOldImages,v $
# Revision 1.4  2014/01/29 21:39:34  dechavez
# Really fixed bug that failed to account for the case of no ISO images
#
# Revision 1.3  2014/01/29 21:29:16  dechavez
# fixed bug that failed to account for the case of no ISO images
#
# Revision 1.2  2013/09/20 17:12:23  dechavez
# removed debug code
#
# Revision 1.1  2013/09/20 17:02:34  dechavez
# created
#
